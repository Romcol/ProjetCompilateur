/*
 *
 *       Auteur: Pedrono Marie-Jo
 *
 * 	création : 13 février 2002
 *
 *     nature : projet etudiant
 *
 */


options{
	JDK_VERSION="1.5";	
}

PARSER_BEGIN(Yaka)



public class Yaka implements Constants {
	public static Declaration declaration = new Declaration();
	public static TabIdent tabIdent = new TabIdent();
	public static Expression expression = new Expression();
	public static Affectation affectation = new Affectation();
	public static YVM yvm = new YVMasm();
	public static Iteration iteration = new Iteration();
	public static Conditionnelle conditionnelle = new Conditionnelle();
	public static Fonction fonction = new Fonction();
	public static AppelFonction appelFonction = new AppelFonction();

	public static void main(String args[]) { 
		Yaka analyseur;
		java.io.InputStream input;

		if (args.length==1) {
			System.out.print(args[args.length-1] + ": \n");
			try {
				input = new java.io.FileInputStream(args[args.length-1]);
			} catch (java.io.FileNotFoundException e) {
				System.out.println("Fichier introuvable.");
				return;
			}
		} else if (args.length==0) {
			System.out.println("Lecture sur l'entree standard...");
			input = System.in;
		} else {
			System.out.println("Usage: java Gram [fichier]");
			return;
		}
		try {
			analyseur = new Yaka(input);
			analyseur.analyse();
			System.out.println("analyse syntaxique reussie!");
		} catch (ParseException e) {
			String msg = e.getMessage();
			msg = msg.substring(0,msg.indexOf("\n"));
			System.out.println("Erreur de syntaxe : "+msg);
		}
	} 
}

PARSER_END(Yaka)
/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS :
{public static String identLu,chaineLue;
 public static int entierLu;
}

/*** Skip whitespace and comments ***/
SKIP :
{
	" "
| "\t"
| "\n"
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
	< ~[] >
}
<IN_COMMENT> SKIP:
{
	 < "*)" >  {SwitchTo(DEFAULT);} 
}


/* Mots réservés*/

TOKEN :
{
	< BOOLEEN: "BOOLEEN" >
| < VAR: "VAR" >
| < FAIRE: "FAIRE" >
| < SINON: "SINON" >
| < POUR: "POUR" >
| < SI: "SI" >
| < FSI: "FSI">
| < ENTIER: "ENTIER" >
| < RETOURNE: "RETOURNE" >
| < VRAI: "VRAI" >
| < TANTQUE: "TANTQUE" >
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME: "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">
}



/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
	< #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ > 
	{ entierLu = Integer.parseInt(image.toString());
				}
| < #lettre: ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
	{identLu =image.toString();	
	}
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
	{ chaineLue = image.toString();
	 }
}


/**************************************/
/********debut de la grammaire ********/
/**************************************/
void analyse() : {}
{
	 <PROGRAMME> <ident> {yvm.entete();}
	 (declFonction())*
	 <PRINCIPAL> {yvm.main();}
	 bloc()
	 <FPRINCIPAL>
	 <FPROGRAMME> { yvm.queue(); }
}

void bloc() : {}{
 (declConst())*
 (declVar())*  { yvm.alloc();}
  suiteInstr()
}

void declFonction() : {}
{
	{ fonction.init(); }

	type()

	 <FONCTION> 
	 <ident> { fonction.setName(YakaTokenManager.identLu); }
	 paramForms() {fonction.noMoreParams();}
	 bloc()
	 <FFONCTION> {fonction.addFunct();}
}

void paramForms() : {}
{
	"("(paramForm()(","paramForm())*)?")"
}

void paramForm() : {}
{
	type()  <ident> { fonction.newParam(YakaTokenManager.identLu); }
}

void declConst() : {}
{
	<CONST>  defConst() ( "," defConst())* ";"
}

void defConst() : {}
{
	
	<ident> {declaration.setCurrentIdent(YakaTokenManager.identLu);}
	 "=" valConst()
		
}

void valConst() : {}{
	<entier>	{declaration.defConstEntier(YakaTokenManager.entierLu);}
 | <ident>	{declaration.defConstIdent(YakaTokenManager.identLu);}
 | <VRAI> 	{declaration.defConstBool(-1);}
 | <FAUX> 	{declaration.defConstBool(0);}
}


void declVar() : {}
{
	<VAR> type()
	<ident> {declaration.defVar(YakaTokenManager.identLu);}
	("," 
			<ident>  {declaration.defVar(YakaTokenManager.identLu);}
	)* ";"
	
}

void type() : {}
{
	 <ENTIER> { declaration.setCurrentType(ValueType.ENTIER); fonction.setReturnType(ValueType.ENTIER); fonction.setCurrentParamType(ValueType.ENTIER);}
 | <BOOLEEN> { declaration.setCurrentType(ValueType.BOOLEEN); fonction.setReturnType(ValueType.BOOLEEN); fonction.setCurrentParamType(ValueType.BOOLEEN);}
}
/*
 * Syntaxe des instructions.
 */
void suiteInstr() : {}
{ (instruction() (";" (instruction())? )*)? }

void instruction() : {}
{
	affectation()
	| lecture()
	| ecriture()
	| iteration()
	| conditionnelle()
	| retourne()
}

void retourne() : {}
{
	<RETOURNE> expression() {fonction.retourne(expression.getFinalType());}
}

void conditionnelle() : {}
{
	<SI> {conditionnelle.open();}
		expression() {conditionnelle.checkExprType(expression.getFinalType());}
	<ALORS> suiteInstr() { conditionnelle.endThen(); }
	(<SINON> suiteInstr())?
	<FSI> {conditionnelle.close();}
}
void iteration() : {}
{
	<TANTQUE> {iteration.open();}
	expression() {iteration.checkExprType(expression.getFinalType());}
	<FAIRE>
		suiteInstr() 
	<FAIT> {iteration.close();}
}
void affectation() : {}
{
	<ident> { affectation.setLeftIdent(YakaTokenManager.identLu); }
	"=" expression() { 
		affectation.setRightType(Yaka.expression.getFinalType()); affectation.eval();}
}

void lecture() : {}
{
	<LIRE> "(" <ident> ")" { yvm.lireEnt(YakaTokenManager.identLu); }
}

void ecriture() : {}
{
	<ECRIRE> "(" (expression() { yvm.ecrireValue(expression.getFinalType()); } | <chaine> { yvm.ecrireChaine(YakaTokenManager.chaineLue); }) ")"
	| <ALALIGNE> { yvm.aLaLigne(); }
}


/*
 * Expression .
 */
void expression() : {}
{
	{ expression.init(); }
	simpleExpr() 
	(opRel() 
	simpleExpr() { expression.eval(); }
	)?
}

void simpleExpr() : {}
{
	 terme() 
	 (opAdd() 
	 terme() { expression.eval();}
	 )*
}

void terme() : {}
{
	facteur() 
	(opMul() 
	facteur()	{expression.eval();}
	)*
}
void facteur() : {}
{   primaire() 
	|  opNeg() primaire() {expression.evalNeg();}
}

void primaire(): {}
{
	 valeur()
 | "(" expression() ")" 
}

void valeur() : {}
{ <entier> 	{expression.pushInt(YakaTokenManager.entierLu);}	
 | <ident> 	{appelFonction.init(YakaTokenManager.identLu);} (argumentsFonction())? {appelFonction.endOfCall();}
 | <VRAI> 	{expression.pushBool(-1);}	
 | <FAUX> 	{expression.pushBool(0);}
}

void argumentsFonction() : {}
{
	"("(expression(){ appelFonction.addArgument(expression.getFinalType()); }
	(","expression() { appelFonction.addArgument(expression.getFinalType()); } )*)?")"
}

void opRel() : {}
{
	 "=" 		{expression.pushOp(Operation.EQUAL);}
 | "<>" 	{expression.pushOp(Operation.NEQUAL);}
 | "<" 		{expression.pushOp(Operation.INF);}
 | "<=" 	{expression.pushOp(Operation.INFEQ);}
 | ">" 		{expression.pushOp(Operation.SUP);}
 | ">=" 	{expression.pushOp(Operation.SUPEQ);}
} 

void opAdd() : {}
{
		"+"		{expression.pushOp(Operation.PLUS);}
	|  "-"	{expression.pushOp(Operation.SUB);}
	| <OU>	{expression.pushOp(Operation.OR);}
}

void opMul() : {}
{
	"*"		{expression.pushOp(Operation.MUL);}
	| "/"		{expression.pushOp(Operation.DIV);}
	| <ET>	{expression.pushOp(Operation.AND);}
} 

void opNeg() : {}
{ "-" 		{expression.pushOp(Operation.MINUS);}
 | <NON>	{expression.pushOp(Operation.NOT);}
}


